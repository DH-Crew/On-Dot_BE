plugins {
    id "org.jetbrains.kotlin.jvm" version "${jetbrainKotlinVersion}"
    id "org.jetbrains.kotlin.plugin.spring" version "${jetbrainKotlinVersion}"
    id "org.jetbrains.kotlin.plugin.jpa" version "${jetbrainKotlinVersion}"
    id "org.jetbrains.kotlin.kapt" version "${jetbrainKotlinVersion}"
    id "org.springframework.boot" version "${springbootVersion}"
    id "io.spring.dependency-management" version "${springDependencyManagementVersion}"
}

group = "${group}"
version = "${version}"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(jvmTarget)
    }
}

def jvmTargetVersion = project.property('jvmTarget')
kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.valueOf("JVM_${jvmTargetVersion}")
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spring.io/milestone' }
    maven { url = 'https://repo.spring.io/snapshot' }
    maven {
        name = 'Central Portal Snapshots'
        url = 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

allOpen {
    annotation("jakarta.persistence.Entity")
    annotation("jakarta.persistence.MappedSuperclass")
    annotation("jakarta.persistence.Embeddable")
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.ai:spring-ai-bom:${springAiVersion}")
    }
}

apply from: "gradle/core.gradle"
apply from: "gradle/db.gradle"
apply from: "gradle/jetbrains.gradle"
apply from: "gradle/monitor.gradle"
apply from: "gradle/spring.gradle"
apply from: "gradle/test.gradle"
